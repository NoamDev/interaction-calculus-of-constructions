// Interaction Calculus of Constructions
// =====================================

// Utils
// -----

(If 0 t f)                   = f
(If 1 t f)                   = t
(Conc String.nil         ys) = ys
(Conc (String.cons x xs) ys) = (String.cons x (Conc xs ys))
(Join List.nil)              = ""
(Join (List.cons x xs))      = (Conc x (Join xs))
(U60.show n)                 = (U60.show.go n String.nil)
(U60.show.go n res)          = (U60.show.go.match (< n 10) n res)
(U60.show.go.match 0 n res)  = (U60.show.go (/ n 10) (String.cons (+ '0' (% n 10)) res))
(U60.show.go.match i n res)  = (String.cons (+ '0' n) res)

// Interactions
// ------------

(APP (Lam   fun.bod) arg) = (fun.bod arg)
(APP (Bri   fun.bod) arg) = (Bri λx(APP (fun.bod (Lam λ$k(x))) (ANN $k arg)))
(APP (Ref _ fun.val) arg) = (APP fun.val arg)
(APP fun             arg) = (App fun arg)

(ANN val (Lam   typ.bod)) = (Lam λx(ANN (APP val $k) (typ.bod (Bri λ$k(x)))))
(ANN val (Bri   typ.bod)) = (typ.bod val)
(ANN val (Ref _ typ.val)) = (ANN val typ.val)
(ANN val typ)             = (Ann val typ)

(REF (Ref a.nam a.val))   = a.val
(REF term)                = term

// Equality
// --------

//(Equal a b dep) = (HVM.print (Join ["Equal: " (Join [(Show a dep) " == " (Show b dep)])]) (Equals a b dep))
(Equal a b dep) = (Equals a b dep)

(Equals (Ref a.nam a.val) b                 dep) = (Equal a.val b dep)
(Equals a                 (Ref b.nam b.val) dep) = (Equal a b.val dep)
(Equals (Ann a.val a.typ) b                 dep) = (Equal a.val b dep)
(Equals a                 (Ann b.val b.typ) dep) = (Equal a b.val dep)
(Equals (Lam a.bod)       (Lam b.bod)       dep) = (Equal (a.bod (Var dep)) (b.bod (Var dep)) (+ dep 1))
(Equals (Bri a.bod)       (Bri b.bod)       dep) = (Equal (a.bod (Var dep)) (b.bod (Var dep)) (+ dep 1))
(Equals (App a.fun a.arg) (App b.fun b.arg) dep) = (& (Equal a.fun b.fun dep) (Equal a.arg b.arg dep))
(Equals (Var a.idx)       (Var b.idx)       dep) = (== a.idx b.idx)
(Equals a                 b                 dep) = 0

// Checking
// --------

(Check term dep) = (HVM.print (Join ["Check: " (Show term dep)]) (Checks term dep))

(Checks (Ref nam val)             dep) = 1
(Checks (Lam bod)                 dep) = (Check (bod (Var dep)) (+ dep 1))
(Checks (App (Ann v t) arg)       dep) = 0
(Checks (App fun arg)             dep) = (& (Check fun dep) (Check arg dep))
(Checks (Bri bod)                 dep) = (Check (bod (Var dep)) (+ dep 1))
(Checks (Ann (Ann val ty0) ty1)   dep) = (& (Equal ty0 ty1 dep) (Check val dep))
(Checks (Ann (Lam bod) typ)       dep) = 0
(Checks (Ann val typ)             dep) = (Check val dep)
(Checks (Var idx)                 dep) = 1

// Encodings
// ---------

// Any ::= θv v
Any = (Bri λx(x))

// (A -> B) ::= θf λx {(f {x: A}): B}
(Fun a b) = (Bri λf(Lam λx(ANN (APP f (ANN x a)) b)))

// (Π(x: A). B[x]) ::= θf λx {(f {x: A}): (B x)}
(All a b) = (Bri λf(Lam λx(ANN (APP f (ANN x a)) (b x))))

// (Πf(x: A). B[x]) ::= θf λx {(f {x: A}): (B x f)}
(Ind a b) = (Bri λf(Lam λx(ANN (APP f (ANN x a)) (b f x))))

// Stringification
// ---------------

(Show (Ref term.nam term.val) dep) = term.nam
(Show (Lam term.bod)          dep) = (Join ["λx" (U60.show dep) " " (Show (term.bod (Var dep)) (+ dep 1))])
(Show (App term.fun term.arg) dep) = (Join ["(" (Show term.fun dep) " " (Show term.arg dep) ")"])
(Show (Bri term.bod)          dep) = (Join ["θx" (U60.show dep) " " (Show (term.bod (Var dep)) (+ dep 1))])
(Show (Ann term.val term.typ) dep) = (Join ["{" (Show term.val dep) " : " (Show term.typ dep) "}"])
(Show (Var term.idx)          dep) = (Join ["x" (U60.show term.idx)])

// Tests
// -----

//Main = 42

//// μNat. ∀(P: *) (Nat -> P) -> P -> P
//Nat = (Fix λnat (All Any λp(All (Fun nat p) λt(All p λf(p)))))

//// ...
//Zero = (Lam λP(Lam λs(Lam λz(z))))

//// ...
//Succ = (Lam λn(Lam λP(Lam λs(Lam λz(APP s n)))))

////// ∀(A: *) ∀(B: *) ∀(A -> B) ∀(B -> A) ∀A A
//TEST = (All Any λa(All Any λb(Fun (Fun a b) (Fun (Fun b a) (Fun a a)))))
//Test = (Lam λA(Lam λB(Lam λab(Lam λba(Lam λa(APP ba (APP ab a))))))) 

//Main = (Check (ANN Test TEST) 0)
//Main = (Check (ANN (Lam λb(b)) (Fun Nat Nat)) 1)

//Check: λx1 {{x1 : Nat} : Nat}
//Check: {{x1 : Nat} : Nat}
//Check: λx3 λx4 λx5 {((({x1 : Nat} x3) λx6 {(x4 {x6 : x2}) : x3}) {x5 : x3}) : x3}
//Check: λx4 λx5 {((({x1 : Nat} x3) λx6 {(x4 {x6 : x2}) : x3}) {x5 : x3}) : x3}
//Check: λx5 {((({x1 : Nat} x3) λx6 {(x4 {x6 : x2}) : x3}) {x5 : x3}) : x3}
//Check: {((({x1 : Nat} x3) λx6 {(x4 {x6 : x2}) : x3}) {x5 : x3}) : x3}
//Check: ((({x1 : Nat} x3) λx6 {(x4 {x6 : x2}) : x3}) {x5 : x3})
//Check: {x5 : x3}
//Check: x5
//Check: (({x1 : Nat} x3) λx6 {(x4 {x6 : x2}) : x3})
//Check: λx6 {(x4 {x6 : x2}) : x3}
//Check: {(x4 {x6 : x2}) : x3}
//Check: (x4 {x6 : x2})
//Check: {x6 : x2}
//Check: x6
//Check: x4
//Check: ({x1 : Nat} x3)










